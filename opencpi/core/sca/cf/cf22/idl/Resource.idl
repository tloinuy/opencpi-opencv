
/*
 *  Copyright (c) Mercury Federal Systems, Inc., Arlington VA., 2009-2010
 *
 *    Mercury Federal Systems, Incorporated
 *    1901 South Bell Street
 *    Suite 402
 *    Arlington, Virginia 22202
 *    United States of America
 *    Telephone 703-413-0781
 *    FAX 703-413-0784
 *
 *  This file is part of OpenCPI (www.opencpi.org).
 *     ____                   __________   ____
 *    / __ \____  ___  ____  / ____/ __ \ /  _/ ____  _________ _
 *   / / / / __ \/ _ \/ __ \/ /   / /_/ / / /  / __ \/ ___/ __ `/
 *  / /_/ / /_/ /  __/ / / / /___/ ____/_/ / _/ /_/ / /  / /_/ /
 *  \____/ .___/\___/_/ /_/\____/_/    /___/(_)____/_/   \__, /
 *      /_/                                             /____/
 *
 *  OpenCPI is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Lesser General Public License as published
 *  by the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  OpenCPI is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public License
 *  along with OpenCPI.  If not, see <http://www.gnu.org/licenses/>.
 */

#ifndef _RESOURCE_IDL
#define _RESOURCE_IDL

#include "LifeCycle.idl"
#include "TestableObject.idl"
#include "PropertySet.idl"
#include "PortSupplier.idl"
#include "ErrorNumberType.idl"

module CF {

  	/* The Resource interface provides a common API for the control and configuration
	of a software component.
	The Resource interface inherits from the LifeCycle, PropertySet, TestableObject, and
	PortSupplier interfaces.
	The inherited LifeCycle, PropertySet, TestableObject, and PortSupplier interface
	operations are documented in their respective sections of this document.
	The Resource interface may also be inherited by other application interfaces
	as described in the Software Profile's Software Component Descriptor (SCD) file. */
	
	interface Resource : LifeCycle, TestableObject, PropertySet, PortSupplier {
		/* The StartError exception indicates that an error occurred during an attempt to start the Resource.  
		The error number indicates an ErrorNumberType value (e.g., EDOM, EPERM, ERANGE). The message is 
		component-dependent, providing additional information describing the reason for the error. */
		
		exception StartError {
			ErrorNumberType errorNumber;
			string msg;
		};
		
		/* The StopError exception indicates that an error occurred during an attempt to stop the Resource.  
		The error number indicates an ErrorNumberType value (e.g., ECANCELED, EFAULT, EINPROGRESS). 
		The message is component-dependent, providing additional information describing the reason for the error. */
		
		exception StopError {
			ErrorNumberType errorNumber;
			string msg;
		};
		
		/* The readonly identifier attribute shall contain the unique identifier for a resource instance. */
		
		readonly attribute string identifier;
		
		/* The start operation puts the Resource in an operating condition. 

		The start operation raises the StartError exception if an error occurs while starting the resource.
		@roseuid 38BEE2457548 */
		void start ()			
			raises (StartError);
			
		/* The stop operation disables all current operations and put the Resource in a non-operating condition.  
		Subsequent configure, query, and start operations are not inhibited by the stop operation.

		The stop operation raises the StopError exception if an error occurs while stopping the resource.
		@roseuid 38BEE2457549 */
		void stop ()			
			raises (StopError);

  };
};

#pragma version CF::Resource 1.0

#endif
